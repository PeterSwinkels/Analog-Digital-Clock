DEFINT A-Z
DECLARE SUB DisplayCanvas (x AS INTEGER, y AS INTEGER, Canvas AS STRING)
DECLARE SUB DisplayText (x AS INTEGER, y AS INTEGER, WidthV AS INTEGER, Text AS STRING)
DECLARE SUB Initialize ()
DECLARE SUB Main ()
DECLARE SUB Quit ()

DIM SHARED Colon AS STRING
DIM SHARED Dash AS STRING
DIM SHARED Digits(0 TO 9) AS STRING

 Initialize
 CALL Main
 Quit
 END

Colon:
 DATA "060320202A20202020202020202020202A202020"
Dash:
 DATA "06032020202020202020CDCDCD20202020202020"
Digits:
 DATA "060320C9CDCDBB2020BA2020BA2020C8CDCDBC20"
 DATA "06032020BB2020202020BA2020202020CA202020"
 DATA "060320C9CDCDBB2020C9CDCDBC2020C8CDCDCD20"
 DATA "060320C9CDCDBB202020CDCDB92020C8CDCDBC20"
 DATA "060320C92020CB2020C8CDCDB92020202020CA20"
 DATA "060320C9CDCDCD2020C8CDCDBB2020C8CDCDBC20"
 DATA "060320C9CDCDBB2020CCCDCDBB2020C8CDCDBC20"
 DATA "060320C9CDCDBB2020202020BA2020202020BC20"
 DATA "060320C9CDCDBB2020CCCDCDB92020C8CDCDBC20"
 DATA "060320C9CDCDBB2020C8CDCDB92020C8CDCDBC20"

SUB DisplayCanvas (x AS INTEGER, y AS INTEGER, Canvas AS STRING)
 WidthV = VAL("&H" + LEFT$(Canvas, 2))
 Height = VAL("&H" + MID$(Canvas, 3, 2))

 LOCATE x
 FOR x2 = 0 TO Height - 1
  LOCATE , y
  FOR y2 = 0 TO WidthV - 1
   PRINT CHR$(VAL("&H" + MID$(Canvas, ((x2 * (WidthV * 2)) + (y2 * 2)) + 5, 2) + "%"));
  NEXT y2
  PRINT
 NEXT x2
END SUB

SUB DisplayText (x AS INTEGER, y AS INTEGER, WidthV AS INTEGER, Text AS STRING)
DIM Character AS STRING

 Column = y
 LOCATE x, Column
 FOR Index = 1 TO LEN(Text)
  Character = MID$(Text, Index, 1)
  SELECT CASE Character
   CASE "0" TO "9"
    DisplayCanvas x, Column, Digits(ASC(Character) - 48)
   CASE ":"
    DisplayCanvas x, Column, Colon
   CASE "-"
    DisplayCanvas x, Column, Dash
  END SELECT
  Column = Column + WidthV
 NEXT Index
END SUB

SUB Initialize
 SCREEN 0
 WIDTH 80, 25
 COLOR 7, 0
 CLS

 RESTORE Digits
 FOR Digit = LBOUND(Digits) TO UBOUND(Digits)
  READ Digits(Digit)
 NEXT Digit

 RESTORE Colon
 READ Colon

 RESTORE Dash
 READ Dash
END SUB

SUB Main
 COLOR 11
 LOCATE 1, 1
 PRINT STRING$(1840, 177)

 LOCATE 3, 4
 PRINT " Clock v1.00 - by: Peter Swinkels, ***2025*** "

 DO
  DisplayText 5, 4, 5, TIME$
  DisplayText 9, 4, 5, DATE$
 LOOP WHILE INKEY$ = ""
END SUB

SUB Quit
 SCREEN 0
 WIDTH 80, 25
 COLOR 7, 0
 CLS
 SYSTEM
END SUB

